apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

sourceSets {
    integrationTest
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'com.android.tools.build:gradle:1.2.0'

    integrationTestCompile gradleApi()
    integrationTestCompile localGroovy()
}

description = 'Clojure plugin for the Gradle-based Android build system'
group = 'org.clojure-android'
version = '0.1.0-SNAPSHOT'

project.ext.compileSdkVersion = 'android-21'
project.ext.buildToolsVersion = '22.0.1'
project.ext.androidSdkDir = getAndroidSdkDir()

publishing {
    repositories {
        maven {
            name = 'test'
            url "${buildDir}/test-repo"
        }
    }
    publications {
        test(MavenPublication) {
            from components.java
            version = "${version}-test"
        }
    }
}

task integrationTest(type: Test) {
    systemProperty 'android.sdk.dir', project.androidSdkDir
    systemProperty 'clojure.plugin.version', "${version}-test"
    systemProperty 'test.maven.repo', publishing.repositories.test.url
    systemProperty 'compile.sdk.version', project.compileSdkVersion
    systemProperty 'build.tools.version', project.buildToolsVersion

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
integrationTest.description 'Runs the integration tests.'
integrationTest.dependsOn ':publishTestPublicationToTestRepository'
integrationTest.group 'verification'
integrationTest.doFirst {
    if (project.androidSdkDir == null) {
        throw new GradleScriptException('No valid ANDROID_HOME detected', null)
    }
}

String getAndroidSdkDir() {
    String androidHome = System.getenv('ANDROID_HOME')
    if (androidHome != null) {
        File f = new File(androidHome)
        if (f.isDirectory()) {
            return f.absolutePath
        }
    }
    return null
}
