apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

sourceSets {
    integrationTest
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation 'com.android.tools.build:gradle:1.2.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation gradleApi()
    testImplementation localGroovy()
    integrationTestImplementation gradleTestKit()
}

description = 'Clojure plugin for the Gradle-based Android build system'
group = 'org.clojure-android'
version = '0.1.0-SNAPSHOT'

ext {
    androidPluginVersion = '3.6.3'
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    androidSdkDir = getAndroidSdkDir()
}

publishing {
    repositories {
        maven {
            name = 'test'
            url "${buildDir}/test-repo"
        }
    }
    publications {
        test(MavenPublication) {
            from components.java
            version = "${version}-test"
        }
    }
}

task integrationTest(type: Test) {
    systemProperty 'android.sdk.dir', project.androidSdkDir
    systemProperty 'clojure.plugin.version', "${version}-test"
    systemProperty 'test.maven.repo', publishing.repositories.test.url
    systemProperty 'android.plugin.version', project.androidPluginVersion
    systemProperty 'compile.sdk.version', project.compileSdkVersion
    systemProperty 'build.tools.version', project.buildToolsVersion

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
integrationTest.description 'Runs the integration tests.'
integrationTest.dependsOn ':publishTestPublicationToTestRepository'
integrationTest.group 'verification'
integrationTest.doFirst {
    if (project.androidSdkDir == null) {
        throw new GradleScriptException('No valid ANDROID_HOME detected', null)
    }
}

String getAndroidSdkDir() {
    String androidHome = System.getenv('ANDROID_HOME')
    if (androidHome != null) {
        File f = new File(androidHome)
        if (f.isDirectory()) {
            return f.absolutePath
        }
    }
    return null
}
